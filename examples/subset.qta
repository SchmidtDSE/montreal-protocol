start default

  define application "dometic refrigeration"

    uses substance "HFC-134a"
      equals 1430 tCO2e / mt

      # Domestic production
      initial charge with 0.12 kg / unit for manufacture
      set manufacture to 350000 kg during year 1
      change manufacture by +5 % each year during years 1 to 5
      change manufacture by +3 % each year during years 6 to 9

      # Trade
      initial charge with 0.30 kg / unit for import
      set import to 90000 kg during year 1
      change import by +5 % each year during years 1 to 5
      change import by +3 % each year during years 6 to 9
      
      # Set prior population
      set priorEquipment to (get equipment as units * 7) units during year 1

      # Service
      retire 6.7 % each year
      recharge 10 % each year with 0.12 kg / unit
    end substance

  end application

end default


start policy "prohibition"

  modify application "dometic refrigeration"

    modify substance "HFC-134a"
      cap manufacture to 75 % during year 3
      cap manufacture to 40 % during year 4
      cap manufacture to 0 % during years 5 to onwards

      cap import to 75 % during year 3
      cap import to 40 % during year 4
      cap import to 0 % during years 5 to onwards
    end substance

  end application

end policy


start policy "reuse"

  modify application "dometic refrigeration"

    modify substance "HFC-134a"
      recover 5 % with 100 % reuse during year 3
      define level as limit (yearAbsolute - 3) * 10 to [0, 30]
      recover level % with 100 % reuse during years 4 to onwards
    end substance

  end application

end policy



start simulations

  simulate "business as usual" from years 1 to 26
  simulate "production policy" using "prohibition" from years 1 to 26
  simulate "reuse policy" using "reuse" from years 1 to 26
  simulate "combined" using "prohibition" then "reuse" from years 1 to 26

end simulations
