start about
  # Name: "Basis for Models"
  # Description: "Translation of in our business BN3."
  # Author: "A Samuel Pottinger"
  # Date: "2024-09-20"
  # Notes: "Based on work from Balaji Natarajan."
end about


start default

  define application "dometic refrigeration"

    uses substance "HFC-134a"
      emit 1430 tCO2e / mt

      # Domestic production
      initial charge with 0.12 kg / unit for manufacture
      set manufacture to 350000 kg during year 1
      change manufacture by +5 % / year during years 1 to 5
      change manufacture by +3 % / year during years 6 to 9

      # Trade
      initial charge with 0.30 kg / unit for import
      set import to 90000 kg during year 1
      change import by +5 % / year during years 1 to 5
      change import by +3 % / year during years 6 to 9

      # Service
      retire 6.7 % / year
      recharge 10 % / year with 0.12 kg / unit

      # Historic units already deployed
      set priorEquipment to (get equipment as units * 7) units during year 1
    end substance

    uses substance "R-600a"
      emit 6 tCO2e / mt

      # Domestic production
      initial charge with 0.05 kg / unit for manufacture
      set manufacture to 200000 kg during year 1
      change manufacture by +8 % / year

      # Trade
      initial charge with 0.05 kg / unit for import
      set import to 10000 kg during year 1
      change import by +8 % / year

      # Service
      retire 6.7 % / year
      recharge 10 % / year with 0.05 kg / unit

      # Historic units already deployed
      set priorEquipment to (get equipment as units * 7) units during year 1
    end substance

    uses substance "low gwp"
      emit 0 tCO2e / mt
    end substance

  end application

  define application "com refrig"

    uses substance "HFC-134a"
      emit 1430 tCO2e / mt

      # Domestic production
      initial charge with 0.30 kg / unit for manufacture
      set manufacture to 90000 kg during year 1
      change manufacture by +5 % / year during years 1 to 5
      change manufacture by +3 % / year during years 6 to 9

      # Trade
      initial charge with 0.30 kg / unit for import
      set import to 90000 kg during year 1
      change import by +5 % / year during years 1 to 5
      change import by +3 % / year during years 6 to 9

      # Service
      retire 6.7 % / year
      recharge 10 % / year with 0.30 kg / unit

      # Historic units already deployed
      set priorEquipment to (get equipment as units * 7) units during year 1
    end substance

    uses substance "R-600a"
      emit 6 tCO2e / mt

      # Domestic production
      initial charge with 0.12 kg / unit for manufacture
      set manufacture to 10000 kg during year 1
      change manufacture by +8 % / year

      # Trade
      initial charge with 0.12 kg / unit for import
      set import to 10000 kg during year 1
      change import by +8 % / year

      # Service
      retire 6.7 % / year
      recharge 10 % / year with 0.12 kg / unit

      # Historic units already deployed
      set priorEquipment to (get equipment as units * 7) units during year 1
    end substance

    uses substance "R-404A"
      emit 3922 tCO2e / mt

      # Domestic production
      initial charge with 0.30 kg / unit for manufacture
      set manufacture to 30000 kg during year 1
      change manufacture by +5 % / year

      # Trade
      initial charge with 0.30 kg / unit for import
      set import to 10000 kg during year 1
      change import by +5 % / year

      # Service
      retire 6.7 % / year
      recharge 10 % / year with 0.12 kg / unit

      # Historic units already deployed
      set priorEquipment to (get equipment as units * 7) units during year 1
    end substance

    uses substance "low gwp"
      emit 0 tCO2e / mt
    end substance

  end application

  define application "res AC"

    uses substance "R-410A"
      emit 2082 tCO2e / mt

      # Domestic production
      initial charge with 0.90 kg / unit for manufacture
      set manufacture to 175000 kg during year 1
      change manufacture by +5 % / year during years 1 to 5
      change manufacture by +3 % / year during years 6 to 9

      # Trade
      initial charge with 0.90 kg / unit for import
      set import to 20000 kg during year 1
      change import by +5 % / year during years 1 to 5
      change import by +3 % / year during years 6 to 9

      # Service
      retire 6.7 % / year
      recharge 10 % / year with 0.90 kg / unit

      # Historic units already deployed
      set priorEquipment to (get equipment as units * 7) units during year 1
    end substance

    uses substance "HFC-32"
      emit 632 tCO2e / mt

      # Domestic production
      initial charge with 0.68 kg / unit for manufacture
      set manufacture to 85000 kg during year 1
      change manufacture by +8 % / year

      # Trade
      initial charge with 0.68 kg / unit for import
      set import to 9000 kg during year 1
      change sales by +8 % / year

      # Service
      retire 6.7 % / year
      recharge 10 % / year with 0.68 kg / unit

      # Historic units already deployed
      set priorEquipment to (get equipment as units * 7) units during year 1
    end substance

    uses substance "R-290"
      emit 6 tCO2e / mt

      # Domestic production
      initial charge with 0.68 kg / unit for manufacture
      set manufacture to 10000 kg during year 1
      change manufacture by +5 % / year

      # Trade
      initial charge with 0.68 kg / unit for import
      set import to 10000 kg during year 1
      change sales by +5 % / year

      # Service
      retire 6.7 % / year
      recharge 10 % / year with 0.35 kg / unit

      # Historic units already deployed
      set priorEquipment to (get equipment as units * 7) units during year 1
    end substance

    uses substance "low gwp"
      emit 0 tCO2e / mt
    end substance

  end application

  define application "mobile AC"

    uses substance "HFC-134a"
      emit 1430 tCO2e / mt

      # Domestic production
      initial charge with 0.90 kg / unit for manufacture
      set manufacture to 175000 kg during year 1
      change manufacture by +5 % / year during years 1 to 5
      change manufacture by +3 % / year during years 6 to 9

      # Trade
      initial charge with 0.90 kg / unit for import
      set import to 20000 kg during year 1
      change import by +5 % / year during years 1 to 5
      change import by +3 % / year during years 6 to 9

      # Service
      retire 6.7 % / year
      recharge 10 % / year with 0.90 kg / unit

      # Historic units already deployed
      set priorEquipment to (get equipment as units * 7) units during year 1
    end substance

    uses substance "R-1234yf"
      emit 6 tCO2e / mt

      # Domestic production
      initial charge with 0.90 kg / unit for manufacture
      set manufacture to 85000 kg during year 1
      change manufacture by +8 % / year

      # Trade
      initial charge with 0.90 kg / unit for import
      set import to 9000 kg during year 1
      change import by +8 % / year

      # Service
      retire 6.7 % / year
      recharge 10 % / year with 0.90 kg / unit

      # Historic units already deployed
      set priorEquipment to (get equipment as units * 7) units during year 1
    end substance

    uses substance "low gwp"
      emit 0 tCO2e / mt
    end substance

  end application

end default


start policy "dom refrig prohibition"

  modify application "dometic refrigeration"

    modify substance "HFC-134a"
      cap manufacture to 75 % during year 3
      cap manufacture to 40 % during year 4
      cap manufacture to 0 % during years 5 to onwards

      cap import to 75 % during year 3
      cap import to 40 % during year 4
      cap import to 0 % during years 5 to onwards
    end substance

  end application

end policy


start policy "dom refrig reuse"

  modify application "dometic refrigeration"

    modify substance "HFC-134a"
      recover 5 % with 100 % reuse during year 3
      define level as limit (yearAbsolute - 3) * 10 to [0, 30]
      recover level % with 100 % reuse during years 4 to onwards
    end substance

  end application

end policy


start policy "dom refrig low-GWP"

  modify application "dometic refrigeration"

    modify substance "HFC-134a"
      define level as limit (yearAbsolute - 2) * 20 to [0, 100]
      replace level % of manufacture with "low gwp" during years 3 to onwards
      replace level % of import with "low gwp" during years 3 to onwards
    end substance

  end application

end policy


start policy "com refrig prohibition"

  modify application "com refrig"

    modify substance "HFC-134a"
      cap manufacture to 0 % during years 3 to onwards
      cap import to 0 % during years 3 to onwards
    end substance

    modify substance "R-404A"
      cap manufacture to 0 % during years 3 to onwards
      cap import to 0 % during years 3 to onwards
    end substance

  end application

end policy


start policy "com refrig reuse"

  modify application "com refrig"

    modify substance "HFC-134a"
      recover 5 % with 100 % reuse during year 3
      define longTermRecovery as limit (yearAbsolute - 3) * 10 to [0, 30]
      recover longTermRecovery % with 100 % reuse during years 4 to onwards
    end substance

  modify substance "R-404A"
      recover 5 % with 100 % reuse during year 3
      define longTermRecovery as limit (yearAbsolute - 3) * 10 to [0, 30]
      recover longTermRecovery % with 100 % reuse during years 4 to onwards
    end substance

  end application

end policy


start policy "com refrig low-GWP"

  modify application "com refrig"

    modify substance "HFC-134a"
      define level as limit (yearAbsolute - 2) * 20 to [0, 100]
      replace level % of manufacture with "low gwp" during years 3 to onwards
      replace level % of import with "low gwp" during years 3 to onwards
    end substance

    modify substance "R-404A"
      define level as limit (yearAbsolute - 2) * 20 to [0, 100]
      replace level % of manufacture with "low gwp" during years 3 to onwards
      replace level % of import with "low gwp" during years 3 to onwards
    end substance

  end application

end policy


start policy "res AC prohibition"

  modify application "res AC"

    modify substance "R-410A"
      cap manufacture to 0 % during years 3 to onwards
      cap import to 0 % during years 3 to onwards
    end substance

    modify substance "HFC-32"
      cap manufacture to 0 % during years 18 to onwards
      cap import to 0 % during years 18 to onwards
    end substance

  end application

end policy


start policy "res AC reuse"

  modify application "com refrig"

    modify substance "R-410A"
      recover 5 % with 100 % reuse during year 3
      define longTermRecovery as limit (yearAbsolute - 3) * 10 to [0, 30]
      recover longTermRecovery % with 100 % reuse during years 4 to onwards
    end substance

  modify substance "HFC-32"
      recover 5 % with 100 % reuse during year 3
      define longTermRecovery as limit (yearAbsolute - 3) * 10 to [0, 30]
      recover longTermRecovery % with 100 % reuse during years 4 to onwards
    end substance

  end application

end policy


start policy "res AC low-GWP"

  modify application "com refrig"

    modify substance "R-410A"
      define level as limit (yearAbsolute - 2) * 20 to [0, 100]
      replace level % of manufacture with "low gwp" during years 3 to onwards
      replace level % of import with "low gwp" during years 3 to onwards
    end substance

    modify substance "HFC-32"
      define level as limit (yearAbsolute - 2) * 20 to [0, 100]
      replace level % of manufacture with "low gwp" during years 3 to onwards
      replace level % of import with "low gwp" during years 3 to onwards
    end substance

  end application

end policy


start policy "mobile AC prohibition"

  modify application "mobile AC"

    modify substance "HFC-134a"
      cap manufacture to 0 % during years 3 to onwards
      cap import to 0 % during years 3 to onwards
    end substance

  end application

end policy


start policy "mobile AC reuse"

  modify application "com refrig"

    modify substance "HFC-134a"
      recover 5 % with 100 % reuse during year 3
      define longTermRecovery as limit (yearAbsolute - 3) * 10 to [0, 30]
      recover longTermRecovery % with 100 % reuse during years 4 to onwards
    end substance

    modify substance "R-1234yf"
      recover 5 % with 100 % reuse during year 3
      define longTermRecovery as limit (yearAbsolute - 3) * 10 to [0, 30]
      recover longTermRecovery % with 100 % reuse during years 4 to onwards
    end substance

  end application

end policy


start policy "mobile AC low-GWP"

  modify application "com refrig"

    modify substance "HFC-134a"
      define level as limit (yearAbsolute - 2) * 20 to [0, 100]
      replace level % of manufacture with "low gwp" during years 3 to onwards
      replace level % of import with "low gwp" during years 3 to onwards
    end substance

  end application

end policy


start simulations

  simulate "business as usual" from years 1 to 29

  simulate "dom refrig high ambition"
    using "dom refrig prohibition"
    then "dom refrig reuse"
    then "dom refrig low-GWP"
  from years 1 to 29

  simulate "com refrig high ambition"
    using "com refrig prohibition"
    then "com refrig reuse"
    then "com refrig low-GWP"
  from years 1 to 29

  simulate "res AC high ambition"
    using "res AC prohibition"
    then "res AC reuse"
    then "res AC low-GWP"
  from years 1 to 29

  simulate "mobile AC high ambition"
    using "mobile AC prohibition"
    then "mobile AC reuse"
    then "mobile AC low-GWP"
  from years 1 to 29

end simulations